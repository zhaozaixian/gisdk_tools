//************************************************************************************
Macro "ExportToSaturn"
    
    SetMapUnits("Meters")
    gNodeLayer="Node"
    gLinkLayer="Link"
    file=OpenFile("c:\\ExportToSaturn.dat","w+")

    WriteLine(file,"*                   File generated by TransCAD  "+GetDateAndTime())
    WriteLine(file,"*――――――――――――――――――――――――――――――――――――――――"+"\n")
    
    WriteLine(file,"&OPTION")
    WriteLine(file,"    UPDATE = FALSE")
    WriteLine(file,"    PASSQ  = FALSE")
    WriteLine(file,"    PLOD   = FALSE")
    WriteLine(file,"&END")
 
    WriteLine(file,"Title:This NetWork Made By ZZX \n      Converted from TransCAD")

    WriteLine(file,"&PARAM")
    WriteLine(file,"    LEFTDR = FALSE")
    WriteLine(file,"    SPEEDS = TRUE")
    WriteLine(file,"    XYUNIT = 1.0")
    WriteLine(file,"    FILGIS = 'Shapes.GIS'")
    WriteLine(file,"    NOMADS = 2       UCNAME(1)='PV'   UCNAME(2)='GV '")                            
    WriteLine(file,"    VCNAME(1)='Car'  VCNAME(2)='Bus'  VCNAME(3)='Truck'")                     
    WriteLine(file,"    VCPCU(1) =1.0    VCPCU(2)=2.5     VCPCU(3)=2.8")                           
    WriteLine(file,"    CINAME(1)='形心连线' CINAME(2)='小区道路'   CINAME(3)='支路'")
    WriteLine(file,"    CINAME(4)='次干道'   CINAME(5)='普通主干道' CINAME(6)='干线主干道'")
    WriteLine(file,"    CINAME(7)='快速路'   CINAME(8)='高速公路'   CINAME(9)='匝道'")
    WriteLine(file,"    CINAME(10)='辅道'    CINAME(11)='辅道出入口' ")
                                       
    WriteLine(file,"&END\n")                                                                    
                                                                                                    
    RunMacro("LinkData",file,gNodeLayer,gLinkLayer)               //333333 RECORDS                    
                                                                                                   
    restrictfilename="c:\\RestrictTurn.bin"                                                      
    RunMacro("Restrict",file,gLinkLayer,restrictfilename)        //444444 RECORDS

    RunMacro("XYCoor",  file,gNodeLayer)                         //555555 RECORDS

    RunMacro("UserClass",file)                                   //888888 RECORDS

    WriteLine(file,"99999")
    WriteLine(file,"99999")

    CloseFile(file)
   
EndMacro


//55555  Node Coordinates***********************************************************************************

Macro "XYCoor"(file,gNodeLayer)

    CentroidList=NULL
    RealnodeList=NULL
    Xmin=99999999
    Ymin=99999999
    Xmax=-999999999
    Ymax=-999999999

    SetLayer(gNodeLayer)
    
    //// Centroids
    qry="Select * where CentroidNode <>null"
    centroidsNUM=SelectByQuery("Centroids", "Several", qry,)
    SortSet("Centroids", "CentroidNode")

    crh=GetFirstRecord(gNodeLayer+"|Centroids",)
    While crh<>NULL Do
      
      cpt=RunMacro("BLToXY",GetPoint(RH2ID(crh)))
      cVal=GetRecordValues(gNodeLayer, , {"CentroidNode"})
      
      if(cpt[1]<Xmin)  then  Xmin=cpt[1]
      if(cpt[1]>Xmax)  then  Xmax=cpt[1]
      if(cpt[2]<Ymin)  then  Ymin=cpt[2]
      if(cpt[2]>Ymax)  then  Ymax=cpt[2]
      	
      CentroidList=InsertArrayElements(CentroidList, ,{{cVal[1][2],cpt[1],cpt[2]}})
      
      crh=GetNextRecord(gNodeLayer+"|Centroids", , )
    End
    
    /////  Real nodes 
    qry="Select * where CentroidNode = NULL"
    realnodesNUM=SelectByQuery("RealNodes", "Several", qry,)
    SortSet("RealNodes", "ID")

    nrh=GetFirstRecord(gNodeLayer+"|RealNodes",)
    While nrh<>NULL Do
      
      npt=RunMacro("BLToXY",GetPoint(RH2ID(nrh)))
      nVal=GetRecordValues(gNodeLayer, , {"ID"})
      
      if(npt[1]<Xmin)  then  Xmin=npt[1]
      if(npt[1]>Xmax)  then  Xmax=npt[1]
      if(npt[2]<Ymin)  then  Ymin=npt[2]
      if(npt[2]>Ymax)  then  Ymax=npt[2]

      RealnodeList=InsertArrayElements(RealnodeList, ,{{nVal[1][2],npt[1],npt[2]}})
      
      nrh=GetNextRecord(gNodeLayer+"|RealNodes", , )
    End

      	
    WriteLine(file,"* Total Nodes: "+String(centroidsNUM+realnodesNUM)+"  in this network")
    WriteLine(file,"* They are: "+String(centroidsNUM)+" Centroids AND "+String(realnodesNUM)+" Realnodes")
    WriteLine(file,"* The  Corrdinate Extents are follows:")
    WriteLine(file,"* XMIN="+String(Xmin)+"    YMIN="+String(Ymin))
    WriteLine(file,"* XMAX="+String(Xmax)+"    YMAX="+String(Ymax))
    WriteLine(file,"* XEXTENT="+String(Xmax-Xmin)+"  YEXTENT="+String(Ymax-Ymin))
     
    WriteLine(file,"*")
    WriteLine(file,"* Coordinate Record Format:")
    WriteLine(file,"* Cols. Description")
    WriteLine(file,"* 1        A‘C’ if columns 2 to 5 contain a zone number;")
    WriteLine(file,"*          or ‘S’ to denote a sector; otherwise blank or part of the node number to")
    WriteLine(file,"*          denote a “real” node.")
    WriteLine(file,"* 2 - 5    The node, zone or sector number;")
    WriteLine(file,"* 6 - 10   Its X co-ordinate (as an integer);")
    WriteLine(file,"* 11- 15   Its Y co-ordinate (as an integer)")
    WriteLine(file,"* 16 -20   (a) For zones, its corresponding sector number")
    WriteLine(file,"*          (b) For nodes, no extra data is currently processed")
    WriteLine(file,"*")
    WriteLine(file,"*        1         2         3         4         5         6         7         8")
    WriteLine(file,"*2345678901234567890123456789012345678901234567890123456789012345678901234567890")
    
    WriteLine(file,"55555")
    
    FOR i=1 to  CentroidList.length  DO 
      
      X=CentroidList[i][2]-Xmin
      Y=CentroidList[i][3]-Ymin
      
      wline = "C"+
              RunMacro("Format", CentroidList[i][1], 4)+
              RunMacro("Format", RealToInt(X+0.5), 5) +
              RunMacro("Format", RealToInt(Y+0.5), 5) 
      WriteLine(file,wline)
    END
    
    FOR i=1 to  RealnodeList.length  DO 
      
      X=RealnodeList[i][2]-Xmin
      Y=RealnodeList[i][3]-Ymin
      
      wline = RunMacro("Format", RealnodeList[i][1], 5)+
              RunMacro("Format", RealToInt(X+0.5), 5) +
              RunMacro("Format", RealToInt(Y+0.5), 5) 
      WriteLine(file,wline)
    END

    WriteLine(file,"99999")

EndMacro


//33333  Buffer links***********************************************************************************

Macro "LinkData"(file,gNodeLayer,gLinkLayer)

       
    WriteLine(file,"*")
    WriteLine(file,"* Network Link Record Format:")
    WriteLine(file,"*")
    WriteLine(file,"* Columns    Contents")
    WriteLine(file,"* 1          A ‘C’ if the following node refers to a zone or a ‘D’ if this is a default speedflow curve;")
    WriteLine(file,"* 2 - 5      The A-node for the link.")                                                    
    WriteLine(file,"* 6          A ‘C’ to indicate a zone number following.")                                   
    WriteLine(file,"* 7 - 10     The B-node for the link.")                                                   
    WriteLine(file,"* 11 -15     The link time (in seconds) or speed (in kph) under free-flow conditions.")  
    WriteLine(file,"* 16 -20     The link time (in seconds) or speed (in kph) at capaCINAMEty.")                 
    WriteLine(file,"* 21 -25     The one-way link capaCINAMEty (in pcus per hour) ")                             
    WriteLine(file,"* 28         A one-way/two-way indicator . ")                                  
    WriteLine(file,"* 29         An ‘S’ if speeds were defined above; otherwise times are assumed.")           
    WriteLine(file,"* 31 -35     The link distance (in metres). ")                                           
    WriteLine(file,"* 36 -40     The power to be used in the link flow-delay curve. (FORMAT - F5.1 - ")      
    WriteLine(file,"*            therefore a deCINAMEmal point must appear in column 39.)")                             
    WriteLine(file,"* 43 -45     A “link index” in the range 0-999; see 5.1.9. ")                          
    WriteLine(file,"* 46C80     (Optionally) up to KNOBS extra data items .")               
    WriteLine(file,"*")                                                                             
    WriteLine(file,"*            RECORD 2 (optional)")                                      
    WriteLine(file,"* Cols.      Description (FREEKY = F; FORMAT 8F10.2) ")                                   
    WriteLine(file,"* 1- 10      First piece of extra data for link A-B ")                                     
    WriteLine(file,"* 11-20      Second piece of extra data etc., etc. with deCINAMEmal points in columns 8, 18,etc.")
    WriteLine(file,"*")
    WriteLine(file,"*        1         2         3         4         5         6         7         8")
    WriteLine(file,"*2345678901234567890123456789012345678901234567890123456789012345678901234567890")

    WriteLine(file,"33333")
    WriteLine(file,"D            99   99 9999   S        0.0    1    * 形心连线")
    WriteLine(file,"D            15    8  500   S        1.8    2    * 小区道路")
    WriteLine(file,"D            20   12  800   S        2.0    3    * 支路")
    WriteLine(file,"D            30   15 1600   S        2.8    4    * 次干道")
    WriteLine(file,"D            45   30 2800   S        3.0    5    * 普通主干道")
    WriteLine(file,"D            60   45 3600   S        3.5    6    * 干线主干道")
    WriteLine(file,"D            80   50 4800   S        4.0    7    * 快速路")
    WriteLine(file,"D           100   65 4800   S        4.5    8    * 高速公路")
    WriteLine(file,"D            30   15  800   S        4.0    9    * 匝道")
    WriteLine(file,"D            45   22 1600   S        3.0   10    * 辅道")
    WriteLine(file,"D            25   15  800   S        3.0   11    * 辅道出入口")
    
    
    SetLayer(gLinkLayer)
    dim linknodes[2]
    
    EnableProgressBar("Export Links", 1)	//设置进度条
    CreateProgressBar("Export Links...","True")
    Step=0
    Count=GetRecordCount(gLinkLayer,)
    StepII=0
    

    lrh=GetFirstRecord(gLinkLayer+"|",)
    While lrh<>null do
    
      StepII=StepII+1
      temp=RealToInt(StepII*100/Count)	//计算进度
      If temp>Step then do
        Step=temp
        stat=UpdateProgressBar("Export Links..." + String(Step),Step)
        If stat="True" Then Do
          DestroyProgressBar()
          GoTo Quit_Loop
        End
      End

      
    lVal=GetRecordValues(gLinkLayer, ,{"Length","Dir","AB_FF_Speed","BA_FF_Speed","AB_CF_Speed","BA_CF_Speed",
                                        "AB_Link_Capacity","BA_Link_Capacity","AB_Power","BA_Power", "AB_CI","BA_CI"})
      For i=1 To lVal.Length Do
        If lVal[i][2]=NULL Then  lVal[i][2]=0
      END
      
      nids=GetEndpoints(RH2ID(lrh))
      
      For i=1 To 2 Do
        nval=GetRecordValues(gNodeLayer,ID2RH(nids[i]),{"CentroidNode"})
        IF(nval[1][2]<>null) THEN   	linknodes[i]="C"+RunMacro("Format", nval[1][2], 4)
                              ELSE	  linknodes[i]=    RunMacro("Format", nids[i],  5 )
      End
      
      
      If lVal[2][2]>=0 then do
        wline = linknodes[1]+linknodes[2] +
                RunMacro("Format", lVal[3][2],5) +  //free  speed
                RunMacro("Format", lVal[5][2],5) +  //capa  speed
                RunMacro("Format", lVal[7][2],5) +  //capaCINAMEty
                "  1S "+                                 //oneway and speed flag
                RunMacro("Format", Round(lVal[1][2],0),5) +  //distance in meters 
                RunMacro("Format", lVal[9][2],5) +  //Power
                RunMacro("Format", lVal[11][2],5)   //capaCINAMEty  index  CINAME
        WriteLine(file,wline)
      End
       
      If lVal[2][2]<=0 then do
        wline = linknodes[2]+linknodes[1] +
                RunMacro("Format", lVal[4][2],5) +  //free  speed
                RunMacro("Format", lVal[6][2],5) +  //capa  speed
                RunMacro("Format", lVal[8][2],5) +  //capaCINAMEty
                "  1S "+                                 //oneway and speed flag
                RunMacro("Format", Round(lVal[1][2],0),5) +  //distance in meters 
                RunMacro("Format", lVal[10][2],5) +  //Power
                RunMacro("Format", lVal[12][2],5)   //capaCINAMEty  index  CINAME
        WriteLine(file,wline)
      End
        
      lrh=GetNextRecord(gLinkLayer+"|", ,)
    End

Quit_Loop:
    DestroyProgressBar()
    WriteLine(file,"99999")

EndMacro


//44444  Restrict Links and Truns***********************************************************************************

Macro "Restrict"(file,gLinkLayer,restrictfilename)

    WriteLine(file,"*")
    WriteLine(file,"* Restrict Links and Turns Record Format:")
    WriteLine(file,"*")
    WriteLine(file,"* Columns    Contents")
    WriteLine(file,"* 1 - 5      The A-node .")                                                    
    WriteLine(file,"* 6 - 10     The B-node .")                                                   
    WriteLine(file,"* 11 -15     The C-node,blank or o in the case of a link.")  
    WriteLine(file,"* 16 -20     The Ban/Penalty/Toll indicator for userclass 1,pv.")                 
    WriteLine(file,"* 21 -25     The Ban/Penalty/Toll indicator for userclass 2,gv,etc .")                             
    WriteLine(file,"*")
    WriteLine(file,"*        1         2         3         4         5         6         7         8")
    WriteLine(file,"*2345678901234567890123456789012345678901234567890123456789012345678901234567890")

    WriteLine(file,"44444")

    SetLayer(gLinkLayer)
/*    
    //turns penalty:   for pv and gv,usally be a banned turn, that is -1
    dim turn[3]
    view = OpenTable("Restrict View", "FFB", {restrictfilename}, {{"Shared", "True"}})   
    view_set = view + "|"
    rec = GetFirstRecord(view_set, null)
    While rec <> null do
       FromlinkID=view.from_link
       ToLinkID  =view.to_link
 
       FromPtIDs=GetEndPoints(FromlinkID)  
       ToPtIDs  =GetEndPoints(ToLinkID)  
    
       if (FromPtIDs[1]=ToPtIDs[1])  then   turn={FromPtIDs[2],FromPtIDs[1],ToPtIDs[2]}
       if (FromPtIDs[1]=ToPtIDs[2])  then   turn={FromPtIDs[2],FromPtIDs[1],ToPtIDs[1]}
       if (FromPtIDs[2]=ToPtIDs[1])  then   turn={FromPtIDs[1],FromPtIDs[2],ToPtIDs[2]}
       if (FromPtIDs[2]=ToPtIDs[2])  then   turn={FromPtIDs[1],FromPtIDs[2],ToPtIDs[1]}

        wline = RunMacro("Format", turn[1],5)+     //from   node
                RunMacro("Format", turn[2],5)+     //center node
                RunMacro("Format", turn[3],5)+     //to    node     
                RunMacro("Format", view.penalty_pv,5)+     //pv_penalty     
                RunMacro("Format", view.penalty_gv,5)      //gv_panalty    
        WriteLine(file,wline)

       rec = GetNextRecord(view_set, null, null)

     END
*/
    //Links Penalty: GV_BAN 
    qry="Select * where GV_Ban <> NULL"
    SelectByQuery("GV_BanLinks", "Several", qry,)
    SortSet("GV_BanLinks", "ID")

    lrh=GetFirstRecord(gLinkLayer+"|GV_BanLinks",)
    While lrh<>NULL Do
      
        nids=GetEndpoints(RH2ID(lrh))
        wline=  RunMacro("Format", nids[1],5)+     //from   node
                RunMacro("Format", nids[2],5)+     //to    node
                RunMacro("Format", -1,15)          //-1:BAN FLAG,Coulum 11-15 for  C node,16-20  fro pv,21-25 for gv
        WriteLine(file,wline)
        
        lrh=GetNextRecord(gLinkLayer+"|GV_BanLinks", , )
      
     End

    //Links Penalty: TOLL for PV and GV
    qry="Select * where Toll_PV <>null OR Toll_GV <> null "
    SelectByQuery("TollLinks", "Several", qry,)
    SortSet("TollLinks", "ID")

    lrh=GetFirstRecord(gLinkLayer+"|TollLinks",)
    While lrh<>NULL Do
      
        nids=GetEndpoints(RH2ID(lrh))
        lval=GetRecordValues(gLinkLayer, ,{"Toll_PV","Toll_GV"})
        
        wline=  RunMacro("Format", nids[1],5)+        // from   node
                RunMacro("Format", nids[2],5)+        //to    node
                "     "                        +       //Coulum 11-15 for  C node
                RunMacro("Format", lval[1][2],5) +   //Coulum 16-20  for pv
                RunMacro("Format", lval[2][2],5)     //Coulum 21-25  for gv
                
        WriteLine(file,wline)
        
        lrh=GetNextRecord(gLinkLayer+"|TollLinks", , )
      
     End

    WriteLine(file,"99999")
     
EndMacro


//88888  Multi_UserClass Definition***********************************************************************************

Macro "UserClass"(file)

    WriteLine(file,"*  UserClass Definition Records")
    WriteLine(file,"*        1         2         3         4         5         6         7         8")
    WriteLine(file,"*2345678901234567890123456789012345678901234567890123456789012345678901234567890")
    WriteLine(file,"*  NO VC M Fact  PPM  PPK KNOB KNOB")
    WriteLine(file,"88888")
    WriteLine(file,"    1 1  1  1.0 0.50 0.50")     //For PV,0.5YUAN per Min,0.5YUAN per kilometer
    WriteLine(file,"    2 1  2  1.0 0.30 0.60")     //For GV,0.3YUAN per Min,0.6YUAN per kilometer
    
    WriteLine(file,"99999")
    
EndMacro


//********************************************************************************************
//********************************************************************************************
Macro "Format"(val, dec)
    str=String(val)
    If StringLength(str)<=dec Then Return(LPad(str, dec))
    Else Do
      dot=Position(str,".")
      If dot=0 Or dot-1>dec Then Return(LPad("#", dec))
      Else Return(Left(str, dec))
    End
EndMacro



//经纬度坐标换算成深圳本地坐标
Macro "BLToXY"(coor)
  B=coor.lat/1000000.0
  L=coor.lon/1000000.0

//WGS84经纬度坐标到北京54坐标
  PAI=3.1415926535898
  a=6378245.0
  e2=0.00669342162297
  e12=0.00673852541468
  p2=3600.0*180.0/PAI
  P0=PAI/180.0
  
  C0=6367558.49686
  C1=32005.79642
  C2=133.86115
  C3=0.7031
  
  l=(L-114)*3600
  t=tan(B*P0)
  t2=t*t
  t4=t2*t2
  Ita2=e12*cos(B*P0)*cos(B*P0)
  Ita4=Ita2*Ita2
  N=a/sqrt(1-e2*sin(B*P0)*sin(B*P0))
  m=l*cos(B*P0)/p2
  
  m2=m*m
  m4=m2*m2
  SinBf=sin(B*P0)
  SinBf2=SinBf*SinBf
  SinBf4=SinBf2*SinBf2
  
  Temp1=C0*B*P0
  Temp2=cos(B*P0)*SinBf*(C1+C2*SinBf2+C3*SinBf4)
  Temp3=1.0/2.0*N*t*m2
  Temp4=1/24.0*(5.0-t2+9*Ita2+4*Ita4)*N*t*m4
  Temp5=1/720*(61-58*t2+t4)*N*t*(m2*m4)
  
  Temp6=N*m
  Temp7=1/6.0*(1-t2+Ita2)*N*(m*m2)
  Temp8=1/120.0*(5-18*t2+(t2*t2)+14*Ita2*Ita2-58*Ita2*t2)*N*(m*m4)
  X=Temp1-Temp2+Temp3+Temp4+Temp5
  Y=500000+Temp6+Temp7+Temp8
    
//从北京54坐标到本地坐标
  PAI=3.1415926535898
  angle=1.00860457
  s=0.997874528635
  dx=-2460040.63895730
  dy=-433223.52500793
  
  angle=angle/180.0*PAI
  
  lx=X*cos(angle)-Y*sin(angle)
  ly=X*sin(angle)+Y*cos(angle)
  
  lx=lx*s
  ly=ly*s
  
  lx=lx+dx
  ly=ly+dy
  
//  lx=Round((lx+3000000)/10000,0)
//  ly=Round((ly+30000000)/10000,0)
//  lx=Round(lx/300,0)
//  ly=Round(ly/200,0)
  
  return({ly,lx})
  
endMacro